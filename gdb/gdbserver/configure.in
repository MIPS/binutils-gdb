dnl Autoconf configure script for GDB server.
dnl Copyright 2000, 2002 Free Software Foundation, Inc.
dnl
dnl This file is part of GDB.
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

dnl Process this file with autoconf to produce a configure script.

AC_INIT(server.c)
AC_CONFIG_HEADER(config.h:config.in)

AC_PROG_CC
AM_PROG_CC_STDC

AC_CANONICAL_SYSTEM

AC_PROG_INSTALL

AC_STDC_HEADERS

AC_CHECK_HEADERS(sgtty.h termio.h termios.h sys/reg.h)

. ${srcdir}/../configure.host

. ${srcdir}/../configure.tgt

frags=
host_makefile_frag=${srcdir}/../config/${gdb_host_cpu}/${gdb_host}.mh
if test ! -f ${host_makefile_frag}; then
	AC_MSG_ERROR("*** GDB remote does not support host ${host}")
fi
frags="$frags $host_makefile_frag"

target_makefile_frag=${srcdir}/../config/${gdb_target_cpu}/${gdb_target}.mt
if test ! -f ${target_makefile_frag}; then
	AC_MSG_ERROR("*** GDB remote does not support target ${target}")
fi
frags="$frags $target_makefile_frag"

AC_SUBST_FILE(host_makefile_frag)
AC_SUBST_FILE(target_makefile_frag)
AC_SUBST(frags)

changequote(,)dnl
hostfile=`sed -n '
s/XM_FILE[	 ]*=[	 ]*\([^	 ]*\)/\1/p
' ${host_makefile_frag}`

targetfile=`sed -n '
s/TM_FILE[	 ]*=[	 ]*\([^	 ]*\)/\1/p
' ${target_makefile_frag}`

if test "${target}" = "${host}"; then
nativefile=`sed -n '
s/NAT_FILE[	 ]*=[	 ]*\([^	 ]*\)/\1/p
' ${host_makefile_frag}`
fi
changequote([,])dnl

AC_OUTPUT(Makefile)
