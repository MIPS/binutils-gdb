# Expect script for AIX 5.2+ tests
#   Copyright 2009 Free Software Foundation
#
# This file is part of the GNU Binutils.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street - Fifth Floor, Boston,
# MA 02110-1301, USA.
#

if { ![istarget "powerpc*-*-aix\[5-9\]*"]
     || [istarget "powerpc*-*-aix5.\[01\]*"] } {
    return
}

# Run a run_link_tests-style test for AIX.  SIZE selects the target size
# (32 or 64).  The other arguments are elements of a run_link_tests test.
#
# Make the following changes before running the test:
#
#    - Mention SIZE in the test name.
#    - Add "-aSIZE --defsym size=SIZE" to the assembler options.
#    - Add the source directory to any "-bI:" and "-bE:" linker options.
#    - Add "-bSIZE" to the linker options.
#    - Add "-XSIZE" to the archiver options.
#    - Replace "SIZE" with SIZE in TOOLS.
#    - When testing 64-bit targets:
#      - Turn tmpdir/aix-* into tmpdir/aix64-*.
#      - Turn tmpdir/libaix-* into tmpdir/libaix64-*.
#      - Turn -laix* into -laix64*, to compensate for the above.
proc run_aix_test { size name ldopts asopts sources tools output } {
    global srcdir subdir

    if { $size == 64 } {
	regsub -all {tmpdir/aix-} $ldopts {tmpdir/aix64-} ldopts
	regsub {^aix} $output {aix64} output

	regsub -all -- {-laix-} $ldopts {-laix64-} ldopts
	regsub {^libaix} $output {libaix64} output
    }
    if { [regexp {.a$} $output] } {
	append ldopts " -X$size"
    } else {
	regsub -all {(-b[IE]):} $ldopts "\\1:$srcdir/$subdir/" ldopts
	append ldopts " -b$size"
    }
    regsub -all {SIZE} $tools $size tools
    run_ld_link_tests [list [list "$name ($size-bit)" \
				 $ldopts \
				 "$asopts -a$size --defsym size=$size" \
				 $sources \
				 $tools \
				 $output]]
}

foreach file { "aix-lineno-1.txt" } {
    remote_upload host "$srcdir/$subdir/$file" "tmpdir/$file"
}

set aix52tests {
    {"Absolute branch test 1"
     "-shared -bI:aix-abs-branch-1.im -bE:aix-abs-branch-1.ex"
     "" {aix-abs-branch-1.s}
     {{objdump {-dR} aix-abs-branch-1.dd}}
     "aix-abs-branch-1.so"}

    {"Relocations against absolute symbols 1"
     "-shared -bI:aix-abs-reloc-1.im -bE:aix-abs-reloc-1.ex"
     {} {aix-abs-reloc-1.s}
     {{objdump -sRj.data aix-abs-reloc-1.od}}
     "aix-abs-reloc-1.so"}

    {"Core sections test 1" "-shared -bE:aix-core-sec-1.ex"
     "" {aix-core-sec-1.s}
     {{objdump -h aix-core-sec-1.hd}}
     "aix-core-sec-1.so"}

    {"Core sections test 2" "-shared -bE:aix-core-sec-2.ex"
     "" {aix-core-sec-2.s}
     {{objdump -h aix-core-sec-2.hd}}
     "aix-core-sec-2.so"}

    {"Core sections test 3" "-shared -bE:aix-core-sec-3.ex"
     "" {aix-core-sec-3.s}
     {{objdump -h aix-core-sec-3.hd}}
     "aix-core-sec-3.so"}

    {"Glink test 1"
     "-shared -bE:aix-glink-1.ex --unresolved-symbols=ignore-all"
     "" {aix-glink-1.s}
     {{objdump {-D -j.text -j.data} aix-glink-1-SIZE.dd}}
     "aix-glink-1.so"}

    {"Line number test 1 (no discards)" "-e.main"
     "" {aix-lineno-1.s}
     {{objdump -dS aix-lineno-1a.dd} {nm {} aix-lineno-1a.nd}}
     "aix-lineno-1a.exe"}

    {"Line number test 1 (discard locals)" "-e.main -x"
     "" {aix-lineno-1.s}
     {{objdump -dS aix-lineno-1b.dd} {nm {} aix-lineno-1b.nd}}
     "aix-lineno-1b.exe"}

    {"TOC test 1" "-shared -bE:aix-toc-1.ex"
     "" {aix-toc-1a.s aix-toc-1b.s}
     {{objdump -dr aix-toc-1-SIZE.dd}}
     "aix-toc-1.so"}
}

foreach test $aix52tests {
    foreach { name ldopts asopts sources tools output } $test {
	run_aix_test 32 $name $ldopts $asopts $sources $tools $output
	run_aix_test 64 $name $ldopts $asopts $sources $tools $output
    }
}
